
plugins {
    id "c"
    id "cpp"
    id "edu.wpi.first.GradleRIO" version "2022.0.0-alpha-2"
}

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcNativeArtifact('frcCpp') {
            targets << "roborio"
            component = 'frcUserProgram'
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
        // Deploy wbot lib
        fileTreeArtifact('wbotLibDeploy') {
            files = fileTree(dir: 'src/shim/wren')
            targets << "roborio"
            directory = '/home/lvuser/wbot'
        }
        // Deploy wren code
        fileTreeArtifact('wbotUserDeploy') {
            files = fileTree(dir: 'src/wren')
            targets << "roborio"
            directory = '/home/lvuser/wren'
        }
    }
}

dependencies {
    // In C++ for now, the argument must be true
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, true)
    simulation wpi.deps.sim.driverstation(wpi.platforms.desktop, true)

    // Websocket extensions require additional configuration.
    // simulation wpi.deps.sim.ws_server(wpi.platforms.desktop, true)
    // simulation wpi.deps.sim.ws_client(wpi.platforms.desktop, true)
}

// Simulation configuration (e.g. environment variables).
sim {
    // Sets the websocket client remote host.
    // envVar "HALSIMWS_HOST", "10.0.0.2"
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            targetPlatform wpi.platforms.desktop

            sources.cpp {
                source {
                    srcDir 'src/shim/cpp'
                }
                exportedHeaders {
                    srcDir 'src/shim/include'

                    srcDir 'deps/wren/src/include'
                    srcDir 'deps/wrenbind17/include'
                    
                    include '**/*.hpp'
                }

                lib library: "wren", linkage: 'static'
            }

            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
        }
        wren(NativeLibrarySpec) {
            targetPlatform wpi.platforms.roborio
            targetPlatform wpi.platforms.desktop

            sources.c {
                source {
                    srcDir 'deps/wren/build'

                    include '**/*.c'
                }
                exportedHeaders {
                    srcDir 'deps/wren/src/include'
                }
            }
        }
    }
}

